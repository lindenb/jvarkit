options {
static=false;
DEBUG_PARSER=false;
IGNORE_CASE=true;
JDK_VERSION="1.8";
}

PARSER_BEGIN(SamFilterParser)
/*
The MIT License (MIT)

Copyright (c) 2017 Pierre Lindenbaum

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

/**

Note to self:

java -cp ~/package/javacc/javacc.jar javacc -OUTPUT_DIRECTORY=src/main/java/com/github/lindenb/jvarkit/util/bio/samfilter -JDK_VERSION=1.8 src/main/resources/javacc/com/github/lindenb/jvarkit/util/bio/samfilter/SamFilterParser.jj 

*/

package com.github.lindenb.jvarkit.util.bio.samfilter;


import htsjdk.samtools.SAMRecord;
import java.util.function.Predicate;
import com.github.lindenb.jvarkit.lang.StringUtils;


public class SamFilterParser {

	
		private final SamRecordFilterFactory factory = new SamRecordFilterFactory();


		
	}
	
PARSER_END(SamFilterParser)

SKIP :
{
" "
| "\t"
| "\n"
| "\r"
}


TOKEN:
{
  <#DIGIT:["0"-"9"]>
| <#LETTER: (["a"-"z"]|"_"|["A"-"Z"])>
| <INT:<DIGIT>  (<DIGIT>)* >
| <OR: "||">
| <AND: "&&">
| <OPAR: "(">
| <CPAR: ")">
| <NOT: "!">
| <#ESCAPE_CHAR: "\\" ["n","t","b","r","f","\\","'","\""] >
| <#ISGET: ("is" | "get") >
| <#READFLAG: ("Read" | "Flag" | "ReadFlag") >
| <SIMPLE_QUOTE_LITERAL:"\'" ( (~["\'","\\","\n","\r"])| <ESCAPE_CHAR>)* "\'" >
| <DOUBLE_QUOTE_LITERAL: "\"" ( (~["\"","\\","\n","\r"])|<ESCAPE_CHAR>)* "\"" >
| <DUPLICATE: (<ISGET>) ? "Duplicate" (<READFLAG>)?>
| <UNMAPPED: (<ISGET>) ? ("Read")? "Unmapped" >
| <MAPPED: (<ISGET>) ? ("Read")? "mapped" >
| <MATEUNMAPPED: (<ISGET>)? "MateUnmapped"  >
| <FAILSVENDORQUALITY: (<ISGET>) ? "FailsVendorQuality" >
| <NOTPRIMARYALIGNMENT: (<ISGET>) ? ("NotPrimaryAlignment"|"SecondaryAlignment") (<READFLAG>)?>
| <SUPPLEMENTARYALIGNMENT: (<ISGET>) ? "SupplementaryAlignment" (<READFLAG>)?>
| <PAIRED: (<ISGET>) ? ("Read")? "Paired" >
| <CLIPPED: (<ISGET>) ? ("Read")? "Clipped" >
| <MAPQUNAVAILABLE: (<ISGET>) ? "MapQ" ("uality")? "Unavailable" >
| <OVERLAP: "overlap" >
| <SAMFLAG: "samFlag" >
| <SAMPLE: "sample" >
| <GROUP: "group" >
| <HASFLAG: "hasFlag" >
| <DISCORDANT: ("is")? "discordant" >
| <MAPQLT:  "mapqlt" >
}

	
Predicate<SAMRecord> anyNode(): { Predicate<SAMRecord> other;}
	{
	(
	  other = NegateExpr() { return other;}
	| other= OrExpr() { return other;}
	| <OPAR> other=anyNode() <CPAR> { return other;}
	)
	}


private Predicate<SAMRecord> NegateExpr(): { }
	{
	<NOT> OrExpr() { return null;}
	}


private Predicate<SAMRecord> OrExpr(): { Predicate<SAMRecord> root,other;}
	{
	root=AndExpr() ( <OR> other=AndExpr() {root = root.or(other); } )*  { return root;}
	}

private Predicate<SAMRecord> AndExpr(): { Predicate<SAMRecord> root,other;}
	{
	root=UnaryExpr() ( <AND> other=UnaryExpr() {root = root.and(other); } )* { return root;}
	}
	
private Predicate<SAMRecord> UnaryExpr(): {String str; Token t; int flg; }
	{
	(
	  <DUPLICATE> <OPAR> <CPAR> { return factory.duplicateFilter(); }
	| <UNMAPPED> <OPAR> <CPAR> { return factory.unmappedFilter();}
	| <MAPPED> <OPAR> <CPAR> { return factory.mappedFilter();}
	| <FAILSVENDORQUALITY> <OPAR> <CPAR> { return factory.failsVendorQuality();}
	| <PAIRED> <OPAR> <CPAR> {  return factory.readPaired();}
	| <CLIPPED> <OPAR> <CPAR> {  return factory.readClipped();}
	| <MAPQUNAVAILABLE> <OPAR> <CPAR> {  return factory.mapqUnavailable();}
	| <MATEUNMAPPED> <OPAR> <CPAR> {  return factory.mateUnmapped();}
	| <NOTPRIMARYALIGNMENT> <OPAR> <CPAR> {  return factory.notPrimaryAlignmentFlag();}
	| <SUPPLEMENTARYALIGNMENT> <OPAR> <CPAR> {  return factory.supplementaryAlignmentFlag();}
	| <OVERLAP> <OPAR>  str=characters() <CPAR> { return factory.overlapBed(str);}
	| <SAMFLAG> <OPAR> t=<INT> <CPAR> { return factory.samFlag(Integer.parseInt(t.image));}
	| <SAMPLE> <OPAR> str=characters() <CPAR>  { return factory.sample(str);}
	| <GROUP> <OPAR> str=characters() <CPAR>  { return factory.group(str);}
	| <HASFLAG> <OPAR> flg=integer() <CPAR> { return factory.hasFlag(flg);}
	| <DISCORDANT> <OPAR> <CPAR> {  return factory.discordant();}
	| <MAPQLT> <OPAR> flg=integer() <CPAR> { return factory.mapqLowerThan(flg);}
	)
	
	}

	
private String characters():
	{
	Token t;
	}
	{
	(
	t=<SIMPLE_QUOTE_LITERAL>
		{
		return  StringUtils.unescapeC(t.image.substring(1,t.image.length()-1));
		}
	| t=<DOUBLE_QUOTE_LITERAL>
		{
		return StringUtils.unescapeC(t.image.substring(1,t.image.length()-1));
		}
	)
	}
private  int integer() : {Token t;}
	{
	t=<INT> { return  Integer.parseInt(t.image); }
	}

	
