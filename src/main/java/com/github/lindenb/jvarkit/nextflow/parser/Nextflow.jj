/*
The MIT License (MIT)

Copyright (c) 2026 Pierre Lindenbaum

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

/*
java -cp /home/lindenb/packages/javacc/bootstrap/javacc.jar  javacc -OUTPUT_DIRECTORY=src/main/java/com/github/lindenb/jvarkit/nextflow/parser src/main/java/com/github/lindenb/jvarkit/nextflow/parser/Nextflow.jj 

*/
options {
	STATIC=false;
	DEBUG_TOKEN_MANAGER=false;
	DEBUG_PARSER=false;
	IGNORE_CASE=false;
	//LOOKAHEAD = 3;
	}

PARSER_BEGIN(NextflowParser)
package com.github.lindenb.jvarkit.nextflow.parser;
import java.util.*;
import java.io.*;
import java.nio.file.*;
import javax.xml.parsers.*;
import javax.xml.parsers.*;
import htsjdk.samtools.util.IOUtil;
import org.w3c.dom.*;

import com.github.lindenb.jvarkit.io.IOUtils;


public class NextflowParser {
  private Document document;
  private Element root;

	private void run() throws IOException, ParseException {
	  	any();
		System.err.println("ok");	
		}
  
  
  
  public static Document parse(Path p) throws IOException, ParseException {
	try {
		IOUtil.assertFileIsReadable(p);
		final DocumentBuilder db = DocumentBuilderFactory.newInstance().newDocumentBuilder();
		final Document dom = db.newDocument();
		final Element root1 =dom.createElement("nextflow");
		dom.appendChild(root1);
		final Element  root=dom.createElement("script");
		root1.appendChild(root);
		root.setAttribute("file",p.toString());

		
		BufferedReader br = null;
		try {
		  	br = IOUtils.openPathForBufferedReading(p);
			final NextflowParser parser = new NextflowParser(br,dom,root);
			parser.run();
			}
		finally {
			if(br!=null) br.close();
			}
		return dom;
		}
	catch(final IOException err) {
		throw err;
		}
	catch(final Throwable err) {
		throw new IOException(err);
		}
	}
  private NextflowParser(Reader r, Document document, Element root) {
	this(r);
	this.document = document;
	this.root= root;
	}
}

PARSER_END(NextflowParser)

SKIP:{
 " "
|  "\t"
|  < "//" (~["\r", "\n"])* >
| < "/*" > : C_COMMENT_STATE
}

<C_COMMENT_STATE> SKIP: {
  < "*/" > : DEFAULT
| < ~[] >   
}



TOKEN:
{
  <PROCESS: "process">
| <INPUT: "input">
| <OUTPUT: "output">
| <SCRIPT: "script">
| <STUB: "stub">
| <TUPLE: "tuple">
| <EMIT: "emit">
| <OPTIONAL: "optional">
| <VAL: "val">
| <PATH: "path">
| <ARITY: "arity">
| <STAGEAS : "stageAs">
| <#LETTER:(["a"-"z"]|["A"-"Z"]|"_")>
| <#DIGIT:["0"-"9"]>
| <INT: <DIGIT>(<DIGIT>)* >
| <IDENTIFIER: <LETTER> (<DIGIT>|<LETTER>)*  >

| <BEGIN_QUOTE3: "\"\"\"" > : IN_QUOTE3
| <#ESCAPE_CHAR : "\\" ["n","t","\\","\"","'","$"] >
| <SIMPLE_QUOTE_LITERAL: "\'" (~["\"","\'","\\","\n","\r"] | "\"" | <ESCAPE_CHAR>)* "\'"  >
| <DOUBLE_QUOTE_LITERAL: "\"" (~["\"","\'","\\","\n","\r"] |"'" | <ESCAPE_CHAR>)* "\""  >
| <  FLOATONE : ((<DIGIT>)+ "." (<DIGIT>)* | (<DIGIT>)* "." (<DIGIT>)+)
              ("e" (["-","+"])? (<DIGIT>)+)? (["f","l"])? >
| <  FLOATTWO : (<DIGIT>)+ "e" (["-","+"])?  (<DIGIT>)+ (["f","l"])? >

| <IGNORE: ("=="|"->"|"!="|"<"|"<="|">"|">="|"!"|"?"|"."|"="|"||"|"|"|"&&"|"&"|"^"|";"|"+"|"-"|"/"|"*"|"^"|"%")>


| <COMMA:",">
| <COLON:":">
| <LPAR:"(">
| <RPAR:")">
| <LBRACK:"{">
| <RBRACK:"}">
| <LARRAY: "[">
| <RARRAY: "]">
| <CR: "\n">
}

<IN_QUOTE3> TOKEN: {
 <END_QUOTE3:"\"\"\"" > : DEFAULT
|< QUOTE3_C:  ~[] >
}



/* http://eriklievaart.com/blog/javacc2.html */

void any() : { Element procE = null; } {
	(procE = process() {root.appendChild(procE);} |ignore_stmt())* <EOF>
	}

private Element process(): {
		Element procE = document.createElement("process");
		Token t;
		Element inputsE = document.createElement("input");
		procE.appendChild(inputsE);
		Element outputsE = document.createElement("output");
		procE.appendChild(outputsE);
		DocumentFragment fragment;
		}
	{
	< PROCESS > t=<IDENTIFIER >  {procE.setAttribute("name",t.image);} <LBRACK > crlf()
		(
		fragment = input_block() {inputsE.appendChild(fragment);}
		| output_block()|stub_block()|script_block()|ignore_stmt())*
	< RBRACK >
		{
		return procE;
		}
	}

void script_block():{}
	{
	<SCRIPT> <COLON> crlf()
	(LOOKAHEAD(2) ignore_stmt())*
	}

void stub_block():{}
	{
	<STUB> <COLON> crlf()
	(LOOKAHEAD(2) ignore_stmt())*
	}

private DocumentFragment input_block():{  DocumentFragment F = 	document.createDocumentFragment(); Element E;}
	{
	<INPUT> <COLON> crlf()
	(E=input_block_item() {F.appendChild(E);} )*
		{
		return F;
		}
	}
void output_block():{}
	{
	<OUTPUT> <COLON> crlf()
	(output_block_item())*
	}
Element input_block_item():{Element E;}
	{
	(E=input_tuple() | E=input_item()) crlf()
	{
	return E;
	}
	}
private Element input_tuple():{Element E = document.createElement("tuple"); Element E2;}
	{
	<TUPLE> E2=input_item() {E.appendChild(E2);} <COMMA>  (crlf())*  E2=input_item() {E.appendChild(E2);} (LOOKAHEAD(2) <COMMA>  (crlf())*  E2=input_item() {E.appendChild(E2);} )*
		{
		return E;
		}
	}

Element input_item():{Element E;}
	{
	(E=val_item()|E=path_item()) {
		return E;
		}
	}

private Element val_item():{Element E= document.createElement("val");Token t;}
	{ 
	<VAL> (<LPAR> t=<IDENTIFIER> {E.setAttribute("name",t.image);} <RPAR> | t=<IDENTIFIER>){E.setAttribute("name",t.image);}
		{
	  	return E;
	  	}
	}
private Element path_item():{Element E= document.createElement("path");Token t;String s;String arity;}
	{
	<PATH>( <LPAR> (
		t = <IDENTIFIER>{E.setAttribute("name",t.image);}|
		s = simple_string(){E.setAttribute("filename",s);}
		(<COMMA> (<ARITY> <COLON> arity=simple_string(){E.setAttribute("arity",arity);}|<STAGEAS> <COLON> simple_string() ))*
		) <RPAR>
		| s = simple_string(){E.setAttribute("filename",s);}
		)
	  	{
	  	return E;
	  	}
	}

void output_block_item():{}
	{
	(output_tuple() | output_item()) (LOOKAHEAD(2) <COMMA>  (crlf())*  output_modifier())* crlf()
	}

void output_modifier():{}
	{
	(emit_modifier()|optional_modifier())
	}

void optional_modifier():{}
	{
	<OPTIONAL> <COLON> <IDENTIFIER>
	}
	
void emit_modifier():{}
	{
	<EMIT> <COLON> (<IDENTIFIER>|<OUTPUT>)
	}

void output_tuple():{}
	{
	<TUPLE> output_item() <COMMA> (crlf())* output_item() (LOOKAHEAD(2) <COMMA>  (crlf())*  output_item())*
	}

private Element output_item():{Element E;}
	{
	(E=val_item()|E=path_item()) {
		return E;
		}
	}



void crlf():{}
	{
	<CR>
	}


String simple_string():{Token t;}
	{
	(t=<SIMPLE_QUOTE_LITERAL> | t=<DOUBLE_QUOTE_LITERAL>) {
		//System.err.println("got string %%"+t.image+"%%");
		return t.image;
		}
	}
void string_block(): {Token t;}
	{
	<BEGIN_QUOTE3>  (t=<QUOTE3_C>)* <END_QUOTE3> 
	}
void ignore_stmt():{}
	{
	(
	  <LPAR> (ignore_stmt())* <RPAR>
	| <LBRACK> (ignore_stmt())*  <RBRACK>
	| <LARRAY> (ignore_stmt())*  <RARRAY>
	| <IDENTIFIER>
	| <COMMA>
	| <COLON>
	| <IGNORE>
	| <INT>
	| <FLOATONE>
	| <FLOATTWO>
	| simple_string()
	| string_block()
	| crlf()
	) 
	}
